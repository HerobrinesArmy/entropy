;INITIATE KERNEL
;Move all initiators here, remove any push/pops, they are no longer needed, remove all SET PC, POPs
;the kernel will simply go down this list to initiate itself,
;then it will deallocate the memory used for these initiators.
;The labels are only used for clarity, they should never be called.

:initiate_kernel



:api_init			;put the api table into place
set a, api_table_length
set i, api_table
set j, 2
jsr ut_copy_mem



:mm_init_a			;allocate all memory, including inits
set [0x0000], 0
set [0x0001], free_space_start



:im_init			;initiate the interrupt manager
hwn a
jsr mm_request_memory
set [im_table_address], a
ias im_interrupt_handler



:fd_init			;initiate the file driver
hwn j
set i, 0x0
set z, fd_drives
:fd_init_loop
 hwq i
 ife a, 0x24c5
  ife b, 0x4fd5
   ife c, 0x000b
    ife x, 0x7e91
     ife y, 0x1eb3
	  set pc, fd_init_add
 add i, 1
 ifn i, j
	set pc, fd_init_loop
set pc, fd_init_next
:fd_init_add
set [z], i
add z, 6
add i, 1
ifn z, fd_drives_end
set pc, fd_init_loop
:fd_init_next
set [fd_drive_cur], [fd_boot_drive]



;:fd_init_test			;It crashes. What is it supposed to do?
;set a, 0x1
;jsr fd_get_free_sector
;set b, [fd_FAT]


:pm_init
SET B, multitasker_Progs_0
ADD B, 10
SET C, 0
:pm_init_loop ;allocate stack for the processes
SET A, 20
JSR mm_request_memory
ADD A, 19
SET [B], A
ADD B, 1
SET [B], A
ADD B, 11
ADD C, 1
IFL C, 15
	SET PC, pm_init_loop

:init
set a, 1
jsr fd_load_prog
set [multitasker_preemption_lock], 0


;DO NOT ALLOCATE MEMORY FROM HERE UNTIL END OF THIS FILE
:mm_init_b			;deallocate initiators
set [0x0000], 0xffff - free_space_start
set [0x0001], -1


:cd_init			;initiate the clock, clock interrupts start after this
hwn i
sub i, 1
:.loop_a
ife i, -1
set pc, .no_clock
hwq i
ife a, 0xb402
ife b, 0x12d0
ife c, 0x0001
ife x, 0x0000
ife y, 0x0000
set pc, .found_clock
std pc, .loop_a
:.no_clock
set [cd_clock_id], -1
sub pc, 1
:.found_clock
set [cd_clock_id], i
set a, i
set b, cd_interrupt_handler
jsr im_request_handler
set b, i
add b, im_hardware_flag
set a, 2
hwi i
set a, 0
set b, tick_length
hwi i



sub pc, 1	;Or else it randomly wanders in the first loaded function. When kernel is working, this should simply never be reached and can be removed.



;:init
;load program /init.bin from disk
;jsr multitasker_addProg
;set [multitasker_preemption_lock], 0
