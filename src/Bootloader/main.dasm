;Bootloader by Lucus

;Loops through all devices until it finds a drive with a bootable disk,
;then loads all reserved sectors from it.
;This will produce a 128 kB file when built.

set PC, 0xff00

.align 0xff00 ;Put actual code in last sector, so it doesn't get overwritten

hwn Z
:loop_a
ife I, Z
set I, 0
hwq I
ife A, 0x24c5
ife B, 0x4fd5
ife C, 0x000b
ife X, 0x7e91
ife Y, 0x1eb3
set PC, m35fd_found
sti PC, loop_a
:m35fd_found
set A, 0
hwi I
ifn B, 1
ifn B, 2
sti PC, loop_a
set A, 2
set X, 0
set Y, 0
hwi I
ifn B, 1
sti PC, loop_a
set A, 0
:loop_b
hwi I
ife B, 3
set PC, loop_b
ifn [0x0], {add PC, 0x11} ;Flags disk bpotable
sti PC, loop_a
;Found bootable disk!
set push, I
set Z, [0x8] ;Number of reserved sectors
sub Z, 1
ifg Z, 0x7e
set Z, 0x7e
set A, 2
set X, 1
set Y, 0x200
:loop_c
ifg X, Z
set PC, boot
hwi I
add X, 1
add Y, 0x200
set A, 0
:loop_c_a
hwi I
ife B, 3
set PC, loop_c_a
set A, 2
set PC, loop_c
:boot
set Z, 0
set Y, 0
set X, 0
set J, 0
set I, 0
set C, 0
set B, 0
set A, 0
set PC, 0
